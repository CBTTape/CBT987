*----------------------------------------------------------------------
*
*        FREEWARE: THIS PROGRAM IS DISTRIBUTED ON AN AS-IS BASIS
*                  WITHOUT WARRANTY, EITHER EXPRESSED OR IMPLIED, BY:
*
*                  THE NATIONAL SYSTEMS PROGRAMMERS ASSOCIATION (NASPA)
*                  P.O. BOX 21525
*                  GREENFIELD, WI 53221
*
*        FIXED BY:  SAM GOLOB  -  MAR 2019   (44-CHAR DATASET NAMES)
*
*----------------------------------------------------------------------
         PRINT OFF
         MACRO
         IEEMBRDS
IEEMBRDS DSECT                         SMF RDS
RDSID    DS    C'RDS '                 CONTROL BLOCK ID
RDSNEXT  DS    A(IEEMBRDS)             FORWARD CHAIN POINTER
RDSPREV  DS    A(IEEMBRDS)             BACKWARD CHAIN POINTER
*
RDSFLAG1 DS    X
RDSFREE  EQU   X'10'                   NEED TO FREE DATASET
RDSDUMP  EQU   X'08'                   OPER HAS BEEN TOLD TO DUMP DS
RDSREADY EQU   X'04'                   DS IS OPEN AND READY
RDSCLOSE EQU   X'02'                   DS NEEDS TO BE CLOSED
RDSNDOP  EQU   X'01'                   DS NEEDS TO BE OPENED
*
RDSFLAG2 DS    X
RDSLOST  EQU   X'80'                   DUMMY RDS FOR LOST DATA
RDSPHYER EQU   X'40'                   PHYSICAL I/O ERROR
RDSLOGER EQU   X'20'                   LOGICAL I/O ERROR
RDSFLAG3 DS    X                       RESERVED
RDSDSLEN DS    AL1(L'RDSNAME)          LENGTH OF DATA SET NAME  HBB5510
RDSNAME  DS    CL44'SYS1.MANX '        DATA SET NAME            HBB5510
RDSVOLID DS    C'SYSRES'               VOLUME SERIAL
         DS    2X                      alignment                HBB5510
RDSDDN   DS    C'SYS00001'             DDNAME RETURNED BY DYNALLOC
RDSCAPTY DS    F                       CAPACITY IN # OF BLOCKS
RDSNXTBL DS    F                       NUMBER OF NEXT AVAIL BLOCK
RDSBQE   DS    A                       ADDR OF BQE BEING WRITTEN
RDSAVSPC DS    F                       AVAIL SPACE FROM SHOWCB
RDSENRBA DS    F                       ENDRBA FROM SHOWCB
RDSCINV  DS    F                       CONTROL INTERVAL SIZE
RDSDOMID DS    F                       MSG # OF DUMP MSG
         DS    F                                                HBB5510
RDSRPL   DS    19F                     VSAM RPL
RDSACB   DS    19F                     VSAM ACB
         MEND
         PRINT ON
         TITLE 'SMFMON - SMF MONITOR'
***********************************************************************
*                                                                     *
*   MODULE NAME:   SMFMON                                             *
*   CSECT  NAME:   SMFMON                                             *
*                                                                     *
*   DESCRIPTION:                                                      *
*        THIS PROGRAM DISPLAYS THE STATUS OF ALL SMF DATASETS.        *
*                                                                     *
*        SMFMON READS THROUGH SYSTEM CONTROL BLOCKS                   *
*        CVT SMCA AND RDS TO DETERMINE THE STATUS OF ALL              *
*        SMF RECORDING DATASETS.                                      *
*                                                                     *
*   AUTHOR:  JEFF GALLAGHER      EAB     NOV. 1981                    *
*                                                                     *
*   FIXED BY SAM GOLOB:  MAR 2019 - FIXED FOR 44-CHAR DATASET NAMES   *
*                                                                     *
***********************************************************************
         EJECT
SMFMON   CSECT
SMFMON   AMODE 31
SMFMON   RMODE 24
         USING SMFMON,R15        BASE REG AT ENTRY
         SPACE 1
         STM   R14,R12,12(R13)   SAVE CALLERS REGS
         B     SKIPID            BR AROUND PROLOG
         SPACE 1
         DC    CL8'SMFMON  '     PROGRAM NAME
         DC    CL8'&SYSDATE'     LAST MAINTENANCE DATE
         SPACE 1
SKIPID   DS    0H
         LR    R12,R15           R12 IS NEW BASE REG.
         DROP  R15
         USING SMFMON,R12        ADDRESSABLITY
         LA    R2,SAVE           ADDRESS OF MY SAVE,
         ST    R2,8(R13)         INTO HIS SAVE,
         ST    R13,4(R2)         AND HIS INTO MINE.
         LR    R13,R2            MAKE MINE CURRENT
         SPACE 2
         USING IEEMBRDS,R5       ADDRESSABILITY
         USING SMCABASE,R3         FOR MAPPING
         USING CVT,R2                DSECTS
*
PRTITLE  DS    0H
         STM   R14,R1,SAVR14T1
         TPUT  PTITLE,72
         LM    R14,R1,SAVR14T1
*
         L     R2,16             CVT
         L     R3,CVTSMCA        SMCA ADDRESS
         TM    CVTSMCA,X'80'     IS BIT ON FOR MVS/SE2?
         BO    SE2                YES, THIS IS REL 2 OF SE
         TPUT  NGSYS,70           NO, COMPLAIN
         B     ALLDONE                  AND EXIT.
         SPACE
SE2      L     R5,SMCASVCR       GET ADDRESS OF CURRENT RDS
         TM    RDSFLAG2,RDSLOST  IS SMF RECORDING?
         BZ    GETFIRST           YES
         TPUT  NOTREC,72          NO... SAY SO
GETFIRST L     R5,SMCAFRDS       GET FIRST RDS
         B     TESTDUMY          CHECK FOR THE DUMMY RDS
GETNEXT  L     R5,RDSNEXT        GET ADDRESS OF NEXT RDS
         CLC   SMCARDSH,0(R5)    IS IT THE HEADER?
         BE    ALLDONE            YES, THATS ALL.
         SPACE 1
TESTDUMY TM    RDSFLAG2,RDSLOST  IS IT THE DUMMY?
         BO    GETNEXT            YES, WE DON'T NEED IT.
         CLI   RDSFLAG1,X'00'     IS THIS ONE "ACTIVE"?
         BNE   CLC                NO, GO AROUND TPUT
         C     R5,SMCASVCR       IS THIS ONE REALLY ACTIVE?
         BNE   NOTAVAIL           NO - ERROR!!
*                                 YES - ISSUE 'NOW RECORDING ON... '
         MVC   RECORDG+24(44),RDSNAME  MOVE DSNAME TO MESSAGE
         TPUT  RECORDG,72 ISSUE THE MESSAGE
CLC      CLC   RDSNXTBL,=F'1'    IS IT EMPTY?
         BE    WTOEMPTY  YES, GO SAY SO
         SPACE 1
         MVC   PERCENT+3(37),RDSNAME  MOVE IN NAME
         LM    R9,R10,RDSCAPTY   GET TOTAL SPACE & LAST USED
         CVD   R9,TOTALSP        PUT IN REGS
         CVD   R10,LASTUSED
         SP    LASTUSED,=PL2'1'  SUBTRACT 1
         MP    LASTUSED,=PL2'100'    MULTIPLY BY 100 FOR PERCENT
         DP    LASTUSED,TOTALSP+4(4) DO THE DIVIDE FOR PERCENT
         MVC   ANS,PATTERN       EDIT ANSWER
         ED    ANS,LASTUSED+2    INTO CHARACTER FORM
         MVC   PERCENT+41(3),ANS+1   AND PUT INTO MESSAGE
         TPUT  PERCENT,72 ISSUE THE MESSAGE
         B     GETNEXT LOOP BACK
         SPACE 2
WTOEMPTY MVC   EMPTY+3(39),RDSNAME   MOVE DSNAME TO MESSAGE
         TPUT  EMPTY,72          ISSUE 'DATASET EMPTY' MESSAGE
         B     GETNEXT           LOOP BACK
         SPACE 2
NOTAVAIL MVC   NOTAV+3(37),RDSNAME  MOVE DSNAME TO MESSAGE
         TPUT  NOTAV,72          ISSUE 'DATASET NOT AVAILABLE' MSG
         B     CLC               LOOP BACK TO COMPARE INST.
         SPACE 2
ALLDONE  L     R13,SAVE+4        RESTORE REG 13
         LM    R14,R12,12(R13)   RESTORE THE REST
         SR    R15,R15           ZERO RETURN CODE
         BR    R14               ALL DONE!
         TITLE 'SMFMON - STORAGE AREAS AND CONSTANTS'
SAVE     DS    18F               SAVE AREA
TOTALSP  DS    D                 DOUBLEWORD WORK AREA
LASTUSED DS    D                 DOUBLEWORD WORK AREA
ANS      DS    CL4               RESULT OF COMPUTATIONS
PATTERN  DC    X'40202120'       EDITING PATTERN
NGSYS    DC    CL72'** MVS SYSTEM MUST BE AT MVS/SE2 OR HIGHER '
NOTREC   DC    CL72'** SMF IS NOT RECORDING !          **'
RECORDG  DC CL72'** SMF NOW RECORDING ON                              '
PERCENT  DC    CL72'**                                       NNN% USED'
EMPTY    DC    CL72'**                                        IS EMPTY'
NOTAV    DC    CL72'**                                       NON-AVAIL'
PTITLE   DC    CL72'**   ----   SMF DATASET STATUS DISPLAY   ----   **'
SAVR14T1 DC    2D'0'
         SPACE 2
         TITLE 'SMFMON - CONTROL BLOCK DSECTS'
         IEEMBRDS
         EJECT
         CVT    DSECT=YES
         EJECT
         IEESMCA
         TITLE 'SMFMON - REGISTER EQUATES'
*
*        R E G I S T E R    E Q U A T E S
*
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
         END
